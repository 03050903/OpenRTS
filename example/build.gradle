apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'groovy'

mainClassName = 'openrts.app.example.MultiplayerGame'
if (!hasProperty('mainClass')) {
	ext.mainClass = 'openrts.app.example.MultiplayerGame'
}

repositories {
	mavenCentral() 
	//maven { url 'https://dl.bintray.com/brainless-studios/tonegodgui' } 
	maven { url "https://jitpack.io" }
}

dependencies {
	compile fileTree(dir: 'lib', include: ['*.jar'])
    compile project(':core')
    compile 'org.codehaus.groovy:groovy-all:2.3.10'
    compile 'com.github.brainless-studios:tonegodgui:92f4924bce'
    compile 'com.github.brainless-studios:tonegodgui-test:c0dd91c1fd'
}

sourceSets {
	 main {
        groovy {
            // override the default locations, rather than adding additional ones
            srcDirs = ['src/main/groovy', 'src/main/java'] 
        }
        java {
            srcDirs = [] // don't compile Java code twice 
        }
		resources { srcDir 'assets' }
    }
}

// remove GroovyCompile->JavaCompile task dependencies
tasks.withType(GroovyCompile) {
	dependsOn = []
}

// add JavaCompile->GroovyCompile task dependencies
tasks.withType(JavaCompile) { task ->
	dependsOn task.name.replace("Java", "Groovy")
}

eclipse {
	classpath {
		downloadSources=true
	 }
	jdt {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	project { 
		name = appName + "-example" 
		natures 'org.springsource.ide.eclipse.gradle.core.nature'		
	}
}
